plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.3.0'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'me.zodd'
version = '1.2-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "Essentials"
        url = "https://repo.essentialsx.net/releases/"
    }
    maven {
        name = "Scarsz"
        url = "https://nexus.scarsz.me/content/repositories/public/"
    }
    maven {
        name = "redlib"
        url = "https://redempt.dev"
    }
    maven {
        // VaultAPI
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    compileOnly 'com.discordsrv:discordsrv:1.28.1'
    compileOnly 'net.dv8tion:JDA:5.0.2'
    compileOnly 'net.essentialsx:EssentialsX:2.20.1'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    implementation 'com.github.Redempt:RedLib:6.6.1'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.runServer {
    minecraftVersion("1.21")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.shadowJar {
    relocate("redempt.redlib","me.zodd.libs.redempt.redlib")
    relocate("redlib","me.zodd.libs.redlib")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
